import org.gradle.api.artifacts.*
apply plugin: 'java'
apply plugin: 'base' // To add "clean" task to the root project.

subprojects {
    apply from: rootProject.file('common.gradle')
}

Properties cfg = new Properties()
cfg.load(new FileInputStream("$rootDir/fxyz-build.properties"))
    
ext {
      artifact_suffix          = cfg.artifact_suffix
      specification_title      = cfg.fxyz_specification_title  //'Java 8u20'
      specification_version    = cfg.fxyz_specification_version //'8.20.7'
      fxyz_name                = 'FXyzLibs'
      fxyz_sampler_name        = 'FXyz-Sampler'
      fxyz_sampler_version     = cfg.fxyz_sampler_specification_version + artifact_suffix
      fxyz_sampler_mainClass   = 'org.fxyz.FXyzClient'
}

// TASKS========================================================================
task mergedJavadoc(type: Javadoc, description: 'Creates Javadoc from all the projects.') {
    title = 'All modules'
    destinationDir = new File(project.buildDir, 'merged-javadoc')

    // Note: The closures below are executed lazily.
    source {
       subprojects*.sourceSets*.main*.allSource
    }
    classpath.from {
        subprojects*.configurations*.compile*.copyRecursive({ !(it instanceof ProjectDependency); })*.resolve()
    }
}

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '2.1'
}

