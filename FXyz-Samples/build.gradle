
    
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

sourceCompatibility = '1.9'
targetCompatibility = '1.9'
    
repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}
import org.gradle.api.artifacts.*
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

//uncomment next line for NativePackages..
//apply from: 'javafx.plugin'


applicationName = 'FXyz-Samples'
mainClassName = fxyz_sampler_mainClass
ext.mainClass = mainClassName
applicationDefaultJvmArgs = ["-XX:+UseConcMarkSweepGC"]

dependencies {
    compile "org.openjfx:javafx-graphics:11:win"
    compile "org.openjfx:javafx-graphics:11:linux"
    compile "org.openjfx:javafx-graphics:11:mac"
        
    compile project(':FXyz-Client')
    compile project(':FXyz-Core')
    compile ('org.reactfx:reactfx:2.0-M5')
    compile ('org.fxmisc.easybind:easybind:1.0.4-SNAPSHOT')
    if (JavaVersion.current().isJava9Compatible()) {
        compile ('org.jfxtras:jfxtras-controls:9.0-r1')
    } else {
        compile ('org.jfxtras:jfxtras-controls:8.0-r6')
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml,javafx.swing'
        ]
    }
    options.encoding = "UTF-8"
}

run {
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls,javafx.fxml,javafx.swing',
                '--add-exports', 'javafx.base/com.sun.javafx.runtime=ALL-UNNAMED',
                '--add-exports', 'javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED'
        ]
    }
}

jar {
    manifest {
        attributes  'Implementation-Title':    'FXyz-Samples',
                    'Implementation-Version':   project.version,
                    'Class-Path':               configurations.compile.collect { it.getName() }.join(' '),
                    'Main-Class':               fxyz_sampler_mainClass,
                    'Automatic-Module-Name' :   "org.fxyz3d.samples"
    }
    from sourceSets.main.allJava
}

shadow{}